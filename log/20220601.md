# rCore 实验第二章

### trap.S 代码解析

```
.altmacro
.macro SAVE_GP n
	sd x\n, \n*8(sp)
.endm

.macro LOAD_GP n
	ld x\n, \n*8(sp)
.endm

.section .text
.global __alltraps
.global __restore
.align 2
__alltraps:
	# 将 sp 保存在 sscratch 中
	# sscratch 中的数据指向临时空间，然后让 sp 指向为那一块地址 
	# 也就是说 sscratch 为原来的栈，现在 sp 为新的栈（进入内核使用的栈）
	csrrw sp, sscratch, sp	
	addi sp, sp, -34 * 8	# 申请 contexts 数据结构的空间
	sd x1, 1 * 8(sp)
	sd x3, 3 * 8(sp)
	.set n, 5	# 跳过 x4，保存 x5 ~ x31
	.rept 27
		SAVE_GP %n
		.set n, n + 1
	.endr
	csrr t0, sstatus	# 保存 sstatus CSR 寄存器到 t0
	csrr t1, sepc
	sd t0, 32 * 8(sp)
	sd t1, 33 * 8(sp)
	# 之前的 csrrw sp, sscratch, sp 指令设置了 sscratch 为原来的栈
	# 也就是发生异常或中断之前的栈
	# 这时候 csrr t2, sscratch 会把用户栈保存在 t2 中
	csrr t2, sscratch
	sd t2, 2 * 8(sp)
	# 至此已经保存好了所有的上下文，就可以开始调用 trap_handler 进行中断处理
	mv a0, sp 
	call trap_handler

__restore:
	# 有两种情况会用到这段函数，这段函数就是上面的逆过程
	# 1. 应用程序第一次运行，同 linux 实现相同
	# 2. 从中断返回到 U 模式
	mv sp, a0
	ld t0, 32 * 8(sp)
	ld t1, 33 * 8(sp)
	ld t2, 2 * 8(sp)
	csrw sstatus, t0
	csrw sepc, t1
	csrw sscratch, t2

	ld x1, 1 * 8(sp)
	ld x3, 3 * 8(sp)
	.set n, 5
	.rept 27
		LOAD_GP %n
		.set n, n + 1
	.endr

	addi sp, sp, 34 * 8
	# csrw sscratch, t2 设置了 sscratch 为用户态的栈
	# 这条指令让 sp 指向用户态的栈，而 sscratch 为内核态的栈
	csrrw sp, sscratch, sp
	sret
```

实际上 riscv 和 x86 这里的实现大部分是相同的

### trap_handler

```
需要根据 trap 的类型来进行处理，系统调用需要手动将 sepc += 4，系统调用有返回值，在执行系统调用时，上下文的 x10 寄存器就是用来保存返回值的，因此这时再调用内核相应的 syscall 得到返回值  
其余的异常类型，直接杀掉这个进程，不用保存上下文，直接开始运行下一个应用程序
与 ucore 不同，这里执行系统调用，可以有返回值
```

### 关于应用程序的运行

```

```

### 实验二测试

```
从测试用例库中将 ch2t_write0.rs 和 ch2_write1.rs 文件复制到项目的 user/src/bin 目录下
需要将测试源码中 use user_lib::{write, STDOUT}; 改为 use user_lib::{write, console::STDOUT};还需要将 console.rs 中 STDOUT 声明为 pub 类型
在运行测试之前需要先进入到 user 目录下执行 make build 指令，编译用户程序

sys_write 安全检查
	1. 针对 fd 文件描述符，应用程序应该知道自己的打开文件表，因此可以在用户态完成检查？？？
	2. 针对非法区域，应用程序不应该知道自己的区域，应用程序认为整个计算机都只有它自己在运行，只有内核才知道应用程序的区域，因此这里应该在内核完成检查，这时需要从 batch.rs 模块获取应用程序的空间，来判断是否越界
```





### 关于 logging 等级控制

```
在我从头开始搭建的项目中，需要多次初始化，但是我把写的代码放到从 git clone 下来的项目中，却完好的运行了
```

